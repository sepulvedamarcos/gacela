' Gambas module file

' Modulo de inicio de la aplicación Gestor de Álbumes Compartidos en Linux Avanzado

Private $configuration As Configuration
Private $log4Gambas As Log4Gambas3
Private $connDB As SQLiteContext
Private $photorepository As PhotoRepository
Private $photoService As PhotoService
Private $compressionService As CompressionService

Public Sub Main()

  Dim response As Integer
  Dim formMain As FMain
  Dim result As String

  $configuration = New Configuration
  $log4Gambas = $configuration.InitializeLogger()
  $log4Gambas.Info("Iniciando Gacela v" & Application.Version)

  ' Inicializa el entorno segun lo definido en el settings
  $configuration.InitializeEnvironment()

  ' Verificamos biblioteca compartida y privilegios. Si no existe se crean
  If Not IsSharedLibraryAvailable() Then
    Message.Warning("Biblioteca compartida no esta configurada")
    Quit
  End If

  ' Inicializacion e inyectores
  $connDB = New SQLiteContext($configuration, $log4gambas)
  ' Crea la clase de capa repository
  $photorepository = New PhotoRepository($configuration, $log4Gambas, $connDB)
  ' Crea el servicio de compresion
  $compressionService = New CompressionService($configuration, $log4gambas)
  ' Crea el servicio de creacion de fotos
  $photoService = New PhotoService($log4gambas, $photorepository, $compressionService, $configuration)

  ' Si es primera vez, insertar datos de prueba
  If $configuration.IsFirstRun Then
    $photoService.InsertSeedData()
  Endif

  ' Creamos e inyectamos en el formulario
  formMain = New FMain($configuration, $log4gambas, $photoService)
  formMain.Show()

Catch
  Print "Se detecto un problema grave y se detendra la aplicación " & Error.Text
  Message.Error("Se detecto un problema grave y se detendra la aplicación " & Error.Text)

End

Private Function IsSharedLibraryAvailable() As Boolean

  Dim hasGroup As Boolean
  Dim hasDirectory As Boolean
  Dim hasPermissions As Boolean
  Dim groupAvailable As String

  ' Verificar que existe el grupo
  Shell "getent group " & Constants.GRUPO_GACELA To groupAvailable
  If groupAvailable = "" Then
    Return False
  Endif

  ' Verificar que el usuario esta en el grupo
  If InStr(groupAvailable, User.Name) <= 0 Then
    Return False
  Endif

  ' Verificar que existe el directorio
  If Not Exist(Constants.GACELA_DIRECTORY_SHARED) Then
    Return False
  Endif

  ' Verificar permisos de escritura
  If Exist(Constants.GACELA_DIRECTORY_SHARED) Then
    Try File.Save(Constants.GACELA_DIRECTORY_SHARED &/ ".test", "test")
    If Error Then Return False
    Try Kill Constants.GACELA_DIRECTORY_SHARED &/ ".test"
  Endif

  'sudo Chown -R root:gacela - users/usr/share/gacela && \
  'sudo Chmod 755 /usr/share/gacela && \
  'sudo Chmod -R 775 /usr/share/gacela/biblioteca && \
  'sudo find /usr/share/gacela/biblioteca -type d -exec Chmod g+s {} \; && \
  'echo "Permisos configurados correctamente"

  Return True

End
