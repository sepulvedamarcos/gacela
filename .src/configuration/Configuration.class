' Gambas class file

' Clase Configuration - Gacela
' Maneja la configuración de la aplicación usando Settings de Gambas

' Constantes que definen la sección de Settings globales
Private Const GLOBAL_VERSION As String = "Global/Version"
Private Const GLOBAL_LIBRARY_TYPE As String = "Global/LibraryType"
Private Const GLOBAL_LIBRARY_DIRECTORY As String = "Global/LibraryDirectory"
Private Const GLOBAL_LIBRARY_PHOTOS_PER_VIEW As String = "Global/PhotosPerView"
Private Const GLOBAL_APPLICATION_THEME As String = "Global/Theme"

' Constantes que definen la seccion de Log4Gambas
Private Const LOG4GAMBAS_LEVEL As String = "Log4Gambas/Level"
Private Const LOG4GAMBAS_OUTPUT As String = "Log4Gambas/Output"
Private Const LOG4GAMBAS_MAXFILES As String = "Log4Gambas/MaxFiles"
Private Const LOG4GAMBAS_SIZELOG As String = "Log4Gambas/SizeLog"
Private Const LOG4GAMBAS_LOGFILE As String = "Log4Gambas/LogFile"
Private Const LOG4GAMBAS_MAXFILESIZE As String = "Log4Gambas/MaxFilesSize"

' Constantes de base de datos y seccion BD
Private Const DB_DATABASE_NAME As String = "Database/Name"
Private Const DB_TYPE As String = "Database/Type"

Public Const DATABASE_NAME As String = "gacela.db"
Public Const TYPE_SQLITE3 As String = "sqlite3"

Public Const TABLE_PHOTOS As String = "photos"
Public Const TABLE_ALBUMS As String = "albums"
Public Const TABLE_PHOTOS_ALBUMS As String = "photo_albums"
Public Const TABLE_TAGS As String = "tags"
Public Const TABLE_PHOTOS_TAGS As String = "photos_tags"

Private Const PATH_APP_MULTI As String = "/usr/share/gacela"

' Enumeración para tipos de biblioteca
Public Enum LibraryType_Private = 1                 '' Enum indica que colección es privada
Public Enum LibraryType_Shared = 2                  '' Enum indica que colección es compartida

' Enumeracion para indicar tipo de tema a usar
Public Enum APP_THEME_SO = 1                        '' Enum para indicar que se usara el thema del Sistema Operativo
Public Enum APP_THEME_LIGHT = 2                     '' Enum para indicar que se usara el thema Light siempre
Public Enum APP_THEME_DARK = 3                      '' Enum para indicar que se usara el thema Dark siempre

' Enumeración para formatos de exportación
' Public Enum ExportFormat_Original = 0
' Public Enum ExportFormat_JPEG_High = 1
' Public Enum ExportFormat_JPEG_Medium = 2
' Public Enum ExportFormat_PNG = 3

' Variable privadas
Private $settings As Settings                       '' Objeto que referencia al archivo de settings
Private $isFirstRun As Boolean = False              '' Indicador de primera ejecucion
Private $useLibrary As Integer = 1                  '' Indicador de tipo de libreria activa
Private $appPath As String                          '' Ruta de los archivos de la aplicación
Private $configName As String = "gacela.conf"       '' Nomrbe del arvhivo de settings
Private $libraryName As String = "gacela.db"        '' Nombre del archivo de SQLite3
Private $dbType As String                           '' Tipo de Bd datos por ahora SQLite3
Private $log4gambas3 As New Log4Gambas3             '' Objeto para hacer logger
Private $soTheme As Integer                         '' Indica el tema activo del SO
Private $appTheme As Integer                        '' Indica el tema que usa la aplicacion

' Propiedades
Property Read IsFirstRun As Boolean                 '' Indicador de primera ejecución
Property Read LibraryPathName As String             '' Indica la ruta donde se alojan los archivos de la aplicacion

'============================================
'
'      Métodos publicos
'
'============================================

Public Sub _new()

  $settings = New Settings(User.Home &/ ".config/gacela/gacela.conf")
  If $settings.Count = 0 Then
    $isFirstRun = True
  Endif

Catch
  Error.Raise("Error inicializando configuración: " & Error.Text)

End

Public Sub InitializeEnvironment() ''Inicializa segun lo que esta previamente definido

  $log4gambas3.warning("Inicializando entorno")

  If $settings[GLOBAL_LIBRARY_TYPE, Null] = Null Then
    Error.Raise("Problemas al cargar configuraciones iniciales")
  Else
    $useLibrary = $settings[GLOBAL_LIBRARY_TYPE]
    $appPath = $settings[GLOBAL_LIBRARY_DIRECTORY]
    $libraryName = $settings[DB_DATABASE_NAME]
    $dbType = $settings[DB_TYPE]
    $soTheme = FindSOTheme()
    $appTheme = $settings[GLOBAL_APPLICATION_THEME, APP_THEME_SO]
  Endif

End

Public Sub InitializePrivateSettings() '' Inicializa y valida la estructura de Settings privada

  $log4gambas3.warning("Inicializando entorno para biblioteca privada")
  ' Cargamos las variable del ambiente seleccionado
  $useLibrary = LibraryType_Private
  $appPath = User.Home &/ ".config/gacela/"
  $libraryName = DATABASE_NAME
  $dbType = TYPE_SQLITE3
  $soTheme = FindSOTheme()
  $appTheme = $settings[GLOBAL_APPLICATION_THEME, APP_THEME_SO]

  InitializeSettings()

End

Public Sub InitializeMultiUserSettings() '' Inicializa y valida la estructura de Settings multiusuaro

  $log4gambas3.warning("Inicializando entorno para biblioteca multiusuaro")
  ' Cargamos las variable del ambiente seleccionado
  $useLibrary = LibraryType_Shared
  $appPath = PATH_APP_MULTI
  $libraryName = DATABASE_NAME
  $dbType = TYPE_SQLITE3
  $soTheme = FindSOTheme()
  $appTheme = $settings[GLOBAL_APPLICATION_THEME, APP_THEME_SO]

  InitializeSettings()

End

Public Function InitializeLogger() As Log4Gambas3

  $log4gambas3.warning("creando archivo ini con datos del log4gambas3")
  ' configuramos el logger
  $log4gambas3.SetAppName(Application.Name)
  $log4gambas3.SetMinLevel($settings[LOG4GAMBAS_LEVEL, $log4gambas3.LEVEL_DEBUG])
  $log4gambas3.SetOutput($settings[LOG4GAMBAS_OUTPUT, $log4gambas3.OUTPUT_CONSOLE])
  $log4gambas3.SetLogFile($settings[LOG4GAMBAS_LOGFILE, $log4gambas3.GetLogFile()])
  $log4gambas3.SetMaxFileSize($settings[LOG4GAMBAS_MAXFILESIZE, $log4gambas3.GetMaxFileSize()])
  $log4gambas3.SetMaxFiles($settings[LOG4GAMBAS_MAXFILES, $log4gambas3.GetMaxFiles()])

  $settings[LOG4GAMBAS_LEVEL] = $log4gambas3.GetMinLevel()
  $settings[LOG4GAMBAS_OUTPUT] = $log4gambas3.GetOutput()
  $settings[LOG4GAMBAS_LOGFILE] = $log4gambas3.GetLogFile()
  $settings[LOG4GAMBAS_MAXFILESIZE] = $log4gambas3.GetMaxFileSize()
  $settings[LOG4GAMBAS_MAXFILES] = $log4gambas3.GetMaxFiles()
  $settings.Save()

  Return $log4gambas3

End

'================================================
'
'         Métodos privados
'
'================================================
Private Sub InitializeSettings()

  Dim configPathName As String = $appPath &/ $configName
  Dim libraryPathName As String

  $log4gambas3.warning("creando archivo ini con datos iniciales")

  ' Verificamos que exista el archivo de Settings
  If Not Exist(configPathName) Then
    If Not Exist($appPath) Then
      Mkdir $appPath
    Endif
  Endif

  ' Sección Global
  $settings[GLOBAL_VERSION] = Application.Version
  $settings[GLOBAL_LIBRARY_TYPE] = LibraryType_Private
  $settings[GLOBAL_LIBRARY_DIRECTORY] = $appPath

  $settings[DB_DATABASE_NAME] = $libraryName
  $settings[DB_TYPE] = $dbType
  $settings.Save()

End

Private Function FindSOTheme() As Integer

  Dim bgColor As Integer
  Dim brightness As Float
  Dim r, g, b As Integer

  bgColor = Color.Background

  ' Extraer componentes RGB
  r = Color[bgColor].Red
  g = Color[bgColor].Green
  b = Color[bgColor].Blue

  ' Calcular brillo (luminosidad)
  brightness = (r * 0.299 + g * 0.587 + b * 0.114)

  ' Si el brillo es menor a 128, es tema oscuro
  If brightness < 128 Then
    Return APP_THEME_DARK
  Else
    Return APP_THEME_LIGHT
  Endif

  Return 0

End

' =======================================================
'
'       Métodos para las propiedades públicas
'
' =======================================================
Function IsFirstRun_Read() As Boolean

  Return $isFirstRun

End

Private Function LibraryPathName_Read() As String

  Return $appPath

End

'
Public Function GetLibraryDirectory() As String

  Return $settings[GLOBAL_LIBRARY_DIRECTORY]

End

Public Function GetDBType() As String

  Return $settings[DB_TYPE]

End

Public Function GetDatabaseName() As String

  Return $settings[DB_DATABASE_NAME]

End
